prefix roo: <http://www.cancerdata.org/roo/>
prefix ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix icd: <http://purl.bioontology.org/ontology/ICD10/>
prefix uo: <http://purl.obolibrary.org/obo/UO_>
prefix sedi: <http://semantic-dicom.org/dcm#>
prefix seq: <http://semantic-dicom.org/seq#>

SELECT ?patient ?gender ?ageDiagnosis ?clinT ?clinN ?ecogStatus ?presDoseGy ?dicom_rtstruct ?derivedVolume ?derivedVolumeVersion ?derivedVolumeDate ?pathT ?pathN
WHERE {
	SERVICE <http://localhost:9999/blazegraph/namespace/kb/sparql> {
		?patient rdf:type ncit:C16960.
		?patient roo:100008 ?disease.
		?disease rdf:type icd:C20.
		
		?patient roo:100301 ?rtRes.
		?rtRes rdf:type ncit:C15313.
		?rtRes roo:100402 ?disease.
		
		?patient roo:100018 ?genderRes.
		?genderRes rdf:type ?gender.
		
		# Get age at diagnosis
		{
			?patient roo:100016 ?ageResDiagnosis.
			?ageResDiagnosis rdf:type roo:100002.
			?ageResDiagnosis roo:100027 ?ageDiagnosisUnitRes.
			?ageDiagnosisUnitRes rdf:type uo:0000036.
			?ageResDiagnosis roo:100042 ?ageDiagnosis.
		}
		
		# Get ECOG performance status
		{
			?patient roo:100218 ?ecogRes.
			?ecogRes rdf:type ?ecogStatus.
		}
		
		# Get clinical TNM values
		{
			?disease roo:100243 ?clinTnmRes.
			?clinTnmRes rdf:type ncit:C48881.
			?clinTnmRes roo:100244 ?clinTRes.
			?clinTRes rdf:type ?clinT.
			?clinTnmRes roo:100242 ?clinNRes.
			?clinNRes rdf:type ?clinN.
		}
		
		# Get pathologic TNM values
		{
			?disease roo:100287 ?pathTnmRes.
			?pathTnmRes rdf:type ncit:C48739.
			?pathTnmRes roo:100288 ?pathTRes.
			?pathTRes rdf:type ?pathT.
			?pathTnmRes roo:100286 ?pathNRes.
			?pathNRes rdf:type ?pathN.
		}
		
		# Get prescribed radiation dose (in Gray)
		{
			?rtRes roo:100023 ?prescribedDoseRes.
			?prescribedDoseRes rdf:type roo:100013.
			?prescribedDoseRes roo:100027 ?presDoseUnitRes.
			?presDoseUnitRes rdf:type uo:000134.
			?prescribedDoseRes roo:100042 ?presDoseGy.
		}
	}
	
	SERVICE <http://localhost:9999/blazegraph/namespace/dicom/sparql> {
		# get all DICOM studies for this patient
		?patient sedi:hasStudy ?dicom_study.
		
		# get all DICOM series (scans) for this patient
		?dicom_study sedi:containsSeries ?dicom_series.
		
		#get the modality per DICOM serie
		?dicom_series sedi:ATT00080060 ?modality.
		
		#filter on the contour objects
		FILTER (?modality = "RTSTRUCT").
		
		# get the actual RTSTRUCT object (file)
		?dicom_series sedi:containsStructureSet ?dicom_rtstruct.
	}
	
	SERVICE <http://localhost:9999/blazegraph/namespace/derived/sparql> {
		# get the volume description resource
		?dicom_rtstruct roo:100315 ?derivedVolumeRes.
		?derivedVolumeRes rdf:type ncit:C94515.
		
		# get / check units of this volume computation
		?derivedVolumeRes roo:100027 ?derivedVolumeUnitRes.
		?derivedVolumeUnitRes rdf:type uo:0000097.
		
		# get version of codebase used to calculate volume
		?derivedVolumeRes roo:100267 ?derivedVolumeVersion.
		?derivedVolumeRes roo:100041 ?derivedVolumeDate.
		
		#get the actual derived volume 
		?derivedVolumeRes roo:100042 ?derivedVolume.
	}
}
ORDER BY ?patient ?dicom_rtstruct
